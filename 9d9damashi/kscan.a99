* Rasmus
* http://atariage.com/forums/topic/162941-assembly-on-the-994a/page-35#entry4233794

*********************************************************************
*
* Check key
*
* r0: Column in MSB: >0000 - >0700, Row in LSB: >0006 - >0014
*
*       Column   0      1    2    3    4    5     6       7
*     Row
*     >0006      =      .    ,    M    N    /    Fire    Fire
*     >0008    Space    L    K    J    H    ;    Left    Left
*     >000A    Enter    O    I    U    Y    P    Right   Right
*     >000C             9    8    7    6    0    Down    Down
*     >000E    Fctn     2    3    4    5    1    Up      Up
*     >0010    Shift    S    D    F    G    A
*     >0012    Ctrl     W    E    R    T    Q
*     >0014             X    C    V    B    Z
*
* On return NEQ means that the key is pressed
*
KEY_CM EQU  >0206
KEY_FS EQU  >0106
KEY_SL EQU  >0506
KEY_EQ EQU  >0006
KEY_0  EQU  >050C
KEY_1  EQU  >050E
KEY_2  EQU  >010E
KEY_3  EQU  >020E
KEY_4  EQU  >030E
KEY_5  EQU  >040E
KEY_6  EQU  >040C
KEY_7  EQU  >030C
KEY_8  EQU  >020C
KEY_9  EQU  >010C
KEY_A  EQU  >0510
KEY_B  EQU  >0414
KEY_C  EQU  >0214
KEY_D  EQU  >0210
KEY_E  EQU  >0212
KEY_F  EQU  >0310
KEY_G  EQU  >0410
KEY_H  EQU  >0408
KEY_I  EQU  >020A
KEY_J  EQU  >0308
KEY_K  EQU  >0208
KEY_L  EQU  >0108
KEY_M  EQU  >0306
KEY_N  EQU  >0406
KEY_O  EQU  >010A
KEY_P  EQU  >050A
KEY_Q  EQU  >0512
KEY_R  EQU  >0312
KEY_S  EQU  >0110
KEY_T  EQU  >0412
KEY_U  EQU  >030A
KEY_V  EQU  >0314
KEY_W  EQU  >0112
KEY_X  EQU  >0114
KEY_Y  EQU  >040A
KEY_Z  EQU  >0514
KEY_SP EQU  >0008
KEY_EN EQU  >000A
KEY_FN EQU  >000E
KEY_SH EQU  >0010
KEY_CT EQU  >0012
JOY_FI EQU  >0606
JOY_LT EQU  >0608
JOY_RT EQU  >060A
JOY_DN EQU  >060C
JOY_UP EQU  >060E
*
check_key:
       li   r12,>0024                  ; CRU address of the column decoder
       ldcr r0,3                       ; Select column
       andi r0,>00ff
       mov  r0,r12                     ; Select row
       tb   0                          ; Test key, EQ if not pressed
       rt
*// check_key

; KPAX - erik olson, 3/9/19
;
; general game keys player 1
; returns:
;  r3 -1,0,1 for up/down
;  r4 -1,0,1 for left/right
;  r5 -1     for fire or fire2
;  r6 -1,1   for - or +=  (speed)
;
; configurable keys
;
KEY_LT  data KEY_S
KEY_RT  data KEY_D
KEY_UP  data KEY_E
KEY_DN  data KEY_X
KEY_FI  data KEY_Q
KEY_FI2 data KEY_FS

kpax:
       mov  r11,r10
	   clr  r3
	   clr  r4
	   clr  r5
       clr  r6
  
kpax1  mov  @KEY_UP,r0
	   bl   @check_key
	   jeq  kpax2
	   seto r3
	   
kpax2  mov  @KEY_DN,r0
	   bl   @check_key
	   jeq  kpax3
	   inc  r3

kpax3  mov  @KEY_LT,r0     ; left
	   bl   @check_key
	   jeq  kpax4
	   seto r4

kpax4  mov  @KEY_RT,r0     ; right
	   bl   @check_key
	   jeq  kpax5
	   inc  r4

kpax5  mov  @KEY_FI,r0
	   bl   @check_key
	   jeq  kpax6
	   seto r5
	   
kpax6  mov  @KEY_FI2,r0
	   bl   @check_key
	   jeq  kpax7
	   seto r5
	   
kpax7  li   r0,KEY_SL     ; /- keys, go slower
	   bl   @check_key
	   jeq  kpax8
	   seto r6
	   
kpax8  li   r0,KEY_EQ     ; =+ keys, go faster
	   bl   @check_key
	   jeq  kpax9
	   inc  r6

kpax9  b    *r10
